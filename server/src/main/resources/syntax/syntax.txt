-- 根句子，下推为多个句项的连接
SEQUENCE -> SEQUENCE_ITEM
SEQUENCE -> SEQUENCE SEQUENCE_ITEM
SEQUENCE -> SEQUENCE & SEQUENCE_ITEM

-- 句项，下推为body、来源标记、减标记的组合
SEQUENCE_ITEM -> SEQUENCE_BODY
SEQUENCE_ITEM -> ^ SEQUENCE_BODY
SEQUENCE_ITEM -> - SEQUENCE_BODY
SEQUENCE_ITEM -> - ^ SEQUENCE_BODY

-- body，下推为元素或注解
SEQUENCE_BODY -> ELEMENT
SEQUENCE_BODY -> ANNOTATION

-- 元素，下推为元素项以及元素项前缀
ELEMENT -> ELEMENT_ITEM
ELEMENT -> ELEMENT_PREFIX ELEMENT_ITEM

-- 注解，由中括号括起，并包含注解项以及元素项前缀
ANNOTATION -> [ ANNOTATION_ITEM ]
ANNOTATION -> [ ELEMENT_PREFIX ANNOTATION_ITEM ]

-- 元素项前缀，包括以下3类前缀
ELEMENT_PREFIX -> @
ELEMENT_PREFIX -> #
ELEMENT_PREFIX -> $

-- 注解项，下推为任意数量的str的|组合
ANNOTATION_ITEM -> str
ANNOTATION_ITEM -> ANNOTATION_ITEM | str
ANNOTATION_ITEM -> ANNOTATION_ITEM / str

-- 元素项，下推为任意数量的SFP的|组合
ELEMENT_ITEM -> SFP
ELEMENT_ITEM -> ELEMENT_ITEM | SFP
ELEMENT_ITEM -> ELEMENT_ITEM / SFP

-- SFP，作为主系表结构有如下下推可能：单一主语；主语和一元系语；主语、系语和表语
SFP -> SUBJECT
SFP -> SUBJECT UNARY_FAMILY
SFP -> SUBJECT FAMILY PREDICATIVE

-- 主语，一定是STRING
SUBJECT -> STRING

-- 一元系语
UNARY_FAMILY -> ~+
UNARY_FAMILY -> ~-

-- 二元系语
FAMILY -> :
FAMILY -> >
FAMILY -> >=
FAMILY -> <
FAMILY -> <=
FAMILY -> ~

-- 表语包括：字符串、集合、范围、排序列表
PREDICATIVE -> STRING
PREDICATIVE -> COLLECTION
PREDICATIVE -> RANGE
PREDICATIVE -> SORT_LIST

-- STRING是多个str的.组合，形成地址串
STRING -> str
STRING -> STRING . str

-- 集合，由一对大括号和集合项组成
COLLECTION -> { }
COLLECTION -> { COLLECTION_ITEM }

-- 集合项是多个str的,组合
COLLECTION_ITEM -> str
COLLECTION_ITEM -> COLLECTION_ITEM , str

-- 范围，由范围起止符号和两个范围str组成
RANGE -> RANGE_BEGIN str , str RANGE_END

-- 范围开始符号
RANGE_BEGIN -> [
RANGE_BEGIN -> (

-- 范围结束符号
RANGE_END -> ]
RANGE_END -> )

-- 排序列表，是多个排序项的,组合
SORT_LIST -> ORDERED_SORT_ITEM
SORT_LIST -> SORT_LIST , ORDERED_SORT_ITEM

-- 排序项，下推至排序项外加+ -符号
ORDERED_SORT_ITEM -> SORT_ITEM
ORDERED_SORT_ITEM -> + SORT_ITEM
ORDERED_SORT_ITEM -> - SORT_ITEM

-- 排序项，包括^的写法
SORT_ITEM -> str
SORT_ITEM -> ^ str